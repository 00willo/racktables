#!/usr/bin/perl

use strict;
use Getopt::Long;
use Net::Telnet;

# fetch command-line parameters
my $op_help;
my $op_port;
my $op_connect_timeout = 2;
my $op_timeout = 10;
my $op_prompt;
my $op_delay = 0;
GetOptions (
    'h' => \$op_help,
    'port:i' => \$op_port,
    'connect-timeout:i' => \$op_connect_timeout,
    'timeout:i' => \$op_timeout,
    'prompt-delay:f' => \$op_delay,
    'prompt:s' => \$op_prompt,
);
if ($op_help) {
    &display_help;
    exit;
}
my $op_host = $ARGV[0];
defined $op_host or die "ERROR: please specify remote host (-h for help)";

sub display_help {
    print <<END;
telnet batch client for RackTables.
Takes commands list in standard input and gives the responses via standard output.
Login credentials are not specially handled and should be placed as first lines of input
Usage: 
$0 {hostname} [--port=X] [--connect-timeout=X] [--prompt=X] [--timeout=X]

port: TCP port number to connect to
connect-timeout: timeout for giving up connecting process, seconds
prompt: command prompt regexp for interactive telnet (auth prompts too)
timeout: wait time for activity of remote telnet peer in seconds

END
}

my $port = $op_port || 23;
my $prompt_re;
if (defined $op_prompt && $op_prompt ne '') {
    $prompt_re = qr/$op_prompt/;
}

my $session = Net::Telnet->new (
    Host => $op_host, 
    Port => $port,
    Timeout => $op_connect_timeout,
);

use IO::Select;
my $sel = new IO::Select($session);

my $buff = '';
my $cmd_finished = 0;
my $nohang_read;
while (! $session->eof) {
	# read output from the device
    $buff .= $session->get (Timeout => $nohang_read ? 0 : $op_timeout, Errmode => $nohang_read ? 'return' : 'die');
    $nohang_read = 0;
	print $1 if ($buff =~ s/(.*\n)//s);
	last if $session->eof;

	# send pending commands to the device
	while (! $cmd_finished) {
		if (defined $prompt_re)
		{
			last if $buff !~ $prompt_re;
			if ($op_delay and IO::Select->select ($sel, undef, undef, $op_delay)) {
				# something is received, no prompt detection at this time
				# set NOHANG options for next reading, cause it can be telnet control sequence
				$nohang_read = 1;
				last;
			}
		}

		my $cmd = <STDIN>;
		if (defined $cmd) {
			# replace all CR and LF symbols with single trailing LF
			$cmd =~ s/[\015\012]//g;
			$cmd .= "\012";
			$session->put($cmd);
		}
		else {
			$cmd_finished = 1;
			$session->shutdown(1); # half-closing TCP connection (nothing to write)
		}
		last if defined ($prompt_re);
	}
}
print $buff;
